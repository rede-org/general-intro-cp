# 5.2  Operables

## Assembling Contexts

As briefly noted [before](../chapter-3-evaluating-with-operations/3.2-expanding-on-when.md#operations-with-multiple-contexts), contexts can be grouped together to form an "Operable". Operables are what `evaluate` actually evaluates to initiate operations. Just like a single context is implicitly cast to a composition for `activate` and `deactivate`, a single context is implicitly cast to an operable for `evaluate`. The grouping of contexts with `< >` prior to an `evaluate` is an explicit in-line declaration of an operable for those contexts.

Similar to [compositions](5.1-compositions.md), operables can be used as declared variables as well, and they can be provided as inputs to an operation. The [FizzBuzz example](../chapter-3-evaluating-with-operations/3.3-operation-groups.md#performing-fizz-buzz) from earlier can provide some familiar material to see how this can be done. Here is the code as it was last seen:

```
Run Fizz Buzz :: when initialized?
    evaluate "Please enter a number." as Console Message,
    evaluate response: Console Response;
    evaluate response as Fizz Buzz Input String.

Evaluate Input :: evaluate a Fizz Buzz Input String and initiate the algorithm
    if the input is valid: <input>
{
    Initiate Fizz Buzz ::
        fizz buzz: input as Fizz Buzz,
        when fizz buzz > 0?
            evaluate fizz buzz;
    
    Notify Of Invalid Input ::
        default?
            evaluate "That input is not valid for Fizz Buzz!" as Console Message;
}.

Perform Fizz Buzz :: perform the Fizz Buzz algorithm: <fizz buzz>
    foreach i in Range[1, fizz buzz]?
{
    Divisible By 15 :: 
        when i % 15?
            evaluate "Fizz Buzz" as Console Message;
    Divisible By 5 :: 
        when i % 5,
        without Divisible By 15?
            evaluate "Buzz" as Console Message;
    Divisible By 3 :: 
        when i % 3,
        without Divisible By 15?
            evaluate "Fizz" as Console Message;
    Otherwise :: 
        default?
            evaluate i to Console Message;
}.
```

The `Perform Fizz Buzz` operation results in some string being output to the console for every count up to the input number. This is completely fine for a straightforward Fizz Buzz implementation, but it can be altered to make the result more dynamic by abstracting the console messages away from `Perform Fizz Buzz`. The first step to do so is to declare operables, in `Initiate Fizz Buzz` of `Evaluate Input`, that will later take the place of any evaluation of a `Console Message`:

```
Evaluate Input :: evaluate a Fizz Buzz Input String and initiate the algorithm
    if the input is valid: <input>
{
    Initiate Fizz Buzz ::
        fizz buzz: input as Fizz Buzz,
        when fizz buzz > 0?
            on fizz buzz: <"Fizz Buzz" as Console Message>;
            on buzz: <"Buzz" as Console Message>;
            on fizz: <"Fizz" as Console Message>;
            on default: <"Neither Fizz Nor Buzz: " as Console Message>;
            evaluate <fizz buzz, on fizz buzz, on buzz, on fizz, on default>;
    
    Notify Of Invalid Input ::
        default?
            evaluate "That input is not valid for Fizz Buzz!" as Console Message;
}.
```

The new lines, like `on fizz buzz: <"Fizz Buzz" as Console Message>`, are declaring operable variables. Any of which can be evaluated to produce the same as `evaluate <"Fizz Buzz" as Console Message>`, which, for a single context, is the same as `evaluate "Fizz Buzz" as Console Message`. The line to start the Fizz Buzz algorithm has also been changed, now being `evaluate <fizz buzz, on fizz buzz, on buzz, on fizz, on default>;`, where there is an in-line operable declaration to group the original `Fizz Buzz` context with the new operables.

{% hint style="warning" %}
Also similar to [compositions](5.1-compositions.md#elements-of-a-composition), the elements of an operable are still operables. So even though an operation may have one of these declared operables input, it can't get the contexts within it out to reference or change directly. It can alter the operable itself though, such as by changing the contexts for different contexts.
{% endhint %}

`Perform Fizz Buzz` will need to update as well, as right now it wouldn't ever be performed with the new `Initiate Fizz Buzz` code:

```
Perform Fizz Buzz :: perform the Fizz Buzz algorithm, evaluating <Console Message> 
    on fizz buzz, <Console Message> on buzz, <Console Message> on fizz, and 
    <Console Messge> otherwise: 
        <fizz buzz, on fizz buzz, on buzz, on fizz, on default>
        foreach i in Range[1, fizz buzz]?
{
    Divisible By 15 :: 
        when i % 15?
            evaluate "Fizz Buzz" as Console Message;
    Divisible By 5 :: 
        when i % 5,
        without Divisible By 15?
            evaluate "Buzz" as Console Message;
    Divisible By 3 :: 
        when i % 3,
        without Divisible By 15?
            evaluate "Fizz" as Console Message;
    Otherwise :: 
        default?
            evaluate i to Console Message;
}.
```

It will be performed with these changes, but it isn't really using the operables. That can be changed by updating all of its `evaluate` calls to use the operables instead of its own `Console Message` contexts:

```
Neither Fizz Nor Buzz Number: context Int.

Perform Fizz Buzz :: perform the Fizz Buzz algorithm, evaluating <Console Message> 
    on fizz buzz, <Console Message> on buzz, <Console Message> on fizz, and 
    <Console Messge> otherwise: 
        <fizz buzz, on fizz buzz, on buzz, on fizz, on default>
        foreach i in Range[1, fizz buzz]?
{
    Divisible By 15 :: 
        when i % 15?
            evaluate on fizz buzz;
    Divisible By 5 :: 
        when i % 5,
        without Divisible By 15?
            evaluate on buzz;
    Divisible By 3 :: 
        when i % 3,
        without Divisible By 15?
            evaluate on fizz;
    Otherwise :: 
        default?
            evaluate on default + i as Neither Fizz Nor Buzz Number;
}.
```

The default `evaluate` is different than might have been expected, now being `evaluate on default i as Neither Fizz Nor Buzz Number` (a new context declared at the top). This isn't appending `i` to the operable's `Console Message`. This is appending `i`, as a `Neither Fizz Nor Buzz Number` context to the operable. For this to evaluate to something, there will need to be a new operation that expects a `Console Message` and a `Neither Fizz Nor Buzz Number`, like so:

```
Output Non Fizz Buzz :: output a Console Message with a Neither Fizz Nor Buzz Number:
    <preceding message, number>?
         evaluate preceding message + number to String.
```

The Fizz Buzz example works again with `Perform Fizz Buzz` no longer being concerned about the specific messages that should be output for each case of the algorithm. This abstraction can go a step further though.



## Operable Types

As with any other type that can be used as a variable, a more specific type can be declared. This would permit abstracting away from `<Console Message>` as an expected operable type to a different type that can be changed over time, without `Perform Fizz Buzz` needing to update for it. The first step to do so is to declare those operable types:

```
On Fizz Buzz: <Console Message>.
On Buzz: <Console Message>.
On Fizz: <Console Message>.
On Default <Console Message>.
```

Now, the operable instance declarations can be updated:

```
Evaluate Input :: evaluate a Fizz Buzz Input String and initiate the algorithm
    if the input is valid: <input>
{
    Initiate Fizz Buzz ::
        fizz buzz: input as Fizz Buzz,
        when fizz buzz > 0?
            on fizz buzz: On Fizz Buzz ["Fizz Buzz" as Console Message];
            on buzz: On Buzz ["Buzz" as Console Message];
            on fizz: On Fizz ["Fizz" as Console Message];
            on default: On Default ["Neither Fizz Nor Buzz: " as Console Message];
            evaluate <fizz buzz, on fizz buzz, on buzz, on fizz, on default>;
    
    Notify Of Invalid Input ::
        default?
            evaluate "That input is not valid for Fizz Buzz!" as Console Message;
}.
```

Then the `Perform Fizz Buzz` declaration:

```
Perform Fizz Buzz :: perform the Fizz Buzz algorithm, evaluating On Fizz Buzz, 
    On Buzz, On Fizz, and On Default as appropriate: 
        <fizz buzz, on fizz buzz, on buzz, on fizz, on default>
        foreach i in Range[1, fizz buzz]?
{
    Divisible By 15 :: 
        when i % 15?
            evaluate on fizz buzz;
    Divisible By 5 :: 
        when i % 5,
        without Divisible By 15?
            evaluate on buzz;
    Divisible By 3 :: 
        when i % 3,
        without Divisible By 15?
            evaluate on fizz;
    Otherwise :: 
        default?
            evaluate on default + i as Neither Fizz Nor Buzz Number;
}.
```

The resulting code is not only more abstract, but also cleaner looking and more readable. `Perform Fizz Buzz` isn't even aware of whether messages are being output anymore, it is purely focused on performing its algorithm up to the input number.

{% hint style="info" %}
`Perform Fizz Buzz` is still concerned with `Neither Fizz Nor Buzz Number` being appended to the `on default` operable though. One way this could be addressed would be to extend the `On Default` operable type with a `Neither Fizz Nor Buzz Number`, with a default context instance, and let `Perform Fizz Buzz` update that part of the operable.
{% endhint %}
