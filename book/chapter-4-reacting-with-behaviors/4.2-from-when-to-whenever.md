# 4.2  From 'When' to 'Whenever'

## A Basic Behavior

When [revisiting Hello World!](4.1-revisiting-hello-world.md), a behavior that uses `Console Output` and `Console Message` was described. While it's great to understand the built-in behaviors of Rede, to really take advantage of Contextual Programming, one must be able to create behaviors. The generalized syntax to do so is very similar to that of an [operation group](../chapter-3-evaluating-with-operations/3.3-operation-groups.md), [as follows](#user-content-fn-1)[^1]:

```
"Behavior Identifier" ::
    "Behavior name with [Context Types]": {"Context Names"} "Qualifications"?
    {
        [An Operation]
        [Another Operation]
    }.
```

There's an optional identifier, a name with specified context types, the names associated to those context types, any qualifications, then the behavior's operations. The symbols used throughout are the same, except the context names are enclosed in `{ }` instead of `< >`.&#x20;

{% hint style="info" %}
As explained [previously](../chapter-3-evaluating-with-operations/3.2-expanding-on-when.md#operations-with-multiple-contexts), the use of `< >` for operations is used in both an operation declaration and to explicitly define an operable to be evaluated. This creates a consistent connection between `< >` being used to signify a concept of operations. It has also been [previously](../chapter-3-evaluating-with-operations/3.3-operation-groups.md#getting-the-input) mentioned ([twice](../chapter-2-creating-context/2.1-organizing-data.md#defining-a-context)) that `{ }` denotes some kind of collection. In the case of a behavior declaration, it is a collection of contexts, which is referred to in Contextual Programming as a 'composition'.

Just as [operables have a purpose](../chapter-6-abstracting-evaluations/6.1-composing-operables.md) for operations, [compositions have a purpose](../chapter-5-evaluating-through-compositions/5.1-composing-contexts.md) which will create consistency behind this specific use of `{ }` for behaviors.
{% endhint %}

### Behavior Operations

Operations within a behavior can look exactly the same as the operations in the examples so far, but most likely they will have additional syntax that generally looks like `for {"Context Names"}`. This addition to the operation declaration specifies what contexts of the behavior are required for the context, which makes the dependencies of the operation more explicit and is necessary for [cross-behavior evaluations](broken-reference).

The overall generalized syntax for an operation usually ends up looking like:

```
"Operation Identifier" ::
    "Operation name with [Context Types]": <"Context Names"> 
        for {"Behavior Context Names"} "Qualifications"? 
            "Operation Logic".
```

### Logging

Logging[^2] is a relatively simple feature that can be implemented and expanded upon as more behavior concepts are described. To start logging, there will need to be a couple of contexts:

```
Log Settings: context.
Log Message: context String.
```

{% hint style="warning" %}
Notice how `Log Settings` has no data at all. It is technically an empty collection of properties, but for all intents and purposes, there is no value associated with it. This kind of context is called a "flag" in Contextual Programming. It does nothing but serve to flag the application as needing to enable some functionality through its existence.

`Log Settings` will have data associated with it later, but for now, it exists only to enable logging within the application.
{% endhint %}

Then the behavior declaration.

```
Logging ::
    Logging per Log Settings for Log Message: {settings, message}?
    {
        
    }.
```

Last, the actual logging, which for now will just be to the console.

```
Logging ::
    Logging per Log Settings for Log Message: {settings, message}?
    {
        Log :: for {message} when initialized?
            evaluate "Log: \(message)" as Console Message,
            \evaluate message as Deregistration;
    }.
```

There's not a lot to this behavior so far. Whenever there's a registered `Log Settings` and a registered `Log Message` that are both available, a new instance of this `Logging` behavior will be created, bound to those contexts. When the behavior initializes (upon its creation), the `Log` operation will be performed using the behavior's `message`. That message will have `Log:` prepended, then the new log will be aliased as a `Console Message` to be output to the console, then the message will be deregistered.&#x20;

Since `\evaluate` is used instead of `evaluate` for the deregistration, the actual deregistration will happen sometime after the operation completes and not as part of it. The behavior exists due to both the settings and the message, so deregistering the message will destroy the behavior instance and free the `Log Settings` up to be bound to a new behavior instance once another `Log Message` has been registered, which would follow the same process.

{% hint style="info" %}
This same functionality could be achieved with a standalone operation, but using a behavior like this will make the functionality easier to extend in the future.
{% endhint %}

The application code to log a message could be:

```
Hello Log :: when initialized?
    evaluate Log Settings as Registration,
    evaluate Log Message ["Hello Log!"] as Registration.
```

In the above code, a `Log Settings` instance is created and registered, followed by a `Log Message` with the value of `"Hello Log!"` also being created and registered. This would be enough to initiate the behavior and output `Hello Log!` to the console.

{% hint style="info" %}
### Registering Together

Recall that behaviors are formed by whichever contexts happen to be available (after being registered). In cases where there are multiple instances of contexts, this may mean that contexts that are registered at different times end up being associated with one another in a behavior, even when other contexts were actually registered near each other, as was just done. This may be fine most of the time, as contexts often don't need to have specific relationships, but sometimes it may be important that they do.

Using the current code as an example, what if it was relevant for this specific `Log Settings` to be associated with this specific `Log Message` when the behavior is created? In that case, they can be registered together, like so:

`Hello Log :: when initialized?` \
&#x20;   evaluate `<Log Settings, Log Message ["Hello Log!"]>` \
&#x20;       `as Registration.`

This code evaluates the contexts together, as [previously shown](../chapter-3-evaluating-with-operations/3.2-expanding-on-when.md#operations-with-multiple-contexts), but after having aliased their grouping as a `Registration`. This will associate the contexts with one another for any behavior considerations. In any place where they could belong to the same behavior, they will, but they will also still qualify for any behaviors where they are needed independent of each other.

While this is important to understand, it won't apply to future uses of the logger.
{% endhint %}

Logging a couple of messages could be done by:

```
Hello Log :: when initialized?
    evaluate Log Settings as Registration,
    evaluate Log Message ["Hello Log!"] as Registration,
    evaluate Log Message ["Another log."] as Registration.
```

In the above code, `Log Settings` and the first `Log Message` create in instance of the `Logging` behavior, resulting in `Hello Log!` being output. That first `Log Message` is then deregistered, which frees the `Log Settings` up to be associated with a new instance of `Logging`, which will occur when the second `Log Message` is registered.



## Conditional Behavior

One of the benefits of logging is having greater control over the type of messages that are logged. This can be achieved by having a log level for the `Log Settings`, to act as a minimum threshold for logs, and a log level for `Log Message`, like so:

<pre><code>Log Level: [Int]  `This is an <a data-footnote-ref href="#user-content-fn-3">enum</a> declaration.`
{
    <a data-footnote-ref href="#user-content-fn-4">Verbose</a>[0],
    <a data-footnote-ref href="#user-content-fn-5">Debug</a>[1],
    <a data-footnote-ref href="#user-content-fn-6">Critical</a>[2],
    <a data-footnote-ref href="#user-content-fn-7">Error</a>[3]
}.

Log Settings: context
{
    Minimum Level: Log Level [Log Level (Debug)];
}.

Log Message: context String
{
    Level: Log Level;
}.
</code></pre>

The `Logging` behavior can be updated to also be conditional now:

```
Logging ::
    Logging per Log Settings for Log Message: {settings, message}
        when message(Level) >= settings(Minimum Level)?
    {
        Log :: for {message} when initialized?
            evaluate "Log: \(message)" as Console Message,
            \evaluate message as Deregistration;
    }.
```

Now, only messages that have at least the minimum log level specified in the `Log Settings` will qualify to create an instance of the behavior and be logged to the console.

{% hint style="warning" %}
Since unqualified `Log Messages` won't be deregistered by the `Logging` behavior's initialization operation, they will still exist in memory waiting to potentially be associated with some future instance of the behavior (in the event `Log Settings (Minimum Level)` is lowered) or some other behavior.

\[Detail how to mitigate possible memory leaks.]
{% endhint %}

\[Show updated initialization operation.]



## Understanding the Cycle



[^1]: As a reminder, text within double quotations here would be replaced by text meaningful to an actual declaration. Text within square brackets signifies that specific names defined within the code should replace the text.

[^2]: A logger is usually an abstraction for where the output of an application will be written, for example, whether it's to the console or a file. Loggers also may provide some control over exactly what is output.

[^3]: Enums are collections of named values, in this case, a collection of named integers. The named values can be referenced as instances of the enum, or the values can be converted to instances of the enum if there is a matching named value.

[^4]: Verbose is the lowest level, it often designates logs that are only useful as information for in-depth tracking of what is happening in the application. Sometimes this level is actually called "Information".

[^5]: Debug designates logs as useful for debugging, but perhaps not necessary to confirm that the application is performing its primary functionality.

[^6]: Critical designates logs as being critical to verifying that the application is functioning. These types of logs are usually important events.

[^7]: Error is for logs that are output as the result of actual errors that occur and should be addressed.
