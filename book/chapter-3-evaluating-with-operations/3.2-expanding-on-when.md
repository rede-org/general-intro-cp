# 3.2  Expanding on 'When'

## Operation Conditions

In the[ Hello World example](3.1-hello-world.md), `when initialized` was introduced as a Circumstantial Control Flow qualification. However, `when` can specify many other qualifications. Most often these qualifications will be conditions that the contexts of an operation must meet for the operation to be performed.

A simple example can be one that checks whether an input number is even. As before, it's best to [define the context](../chapter-2-creating-context/2.1-organizing-data.md#defining-a-context) that we will use first:

```
Input Number: context Int.
```

Then the entry point to the application, where the user will be asked to provide a number:

```
Check whether even: when initialized?
    evaluate "Please enter a number." as Console Message,
    evaluate response: Console Response;
    evaluate response to Input Number.
```

{% hint style="warning" %}
Notice how `response` is being mapped to an `Input Number` using the `to` keyword. `response` is a `Console Response` which is an alias for a `String` which, in Rede, has a built-in mapping to an `Int` which `Input Number` is an alias for. The full steps of what is occurring here are: `response` being checked for a direct mapping to `Input Number`, when this isn't found `response` is checked for a direct mapping to `Int`, when this isn't found `response` is de-aliased as a `String` which is checked for a mapping to `Input Number`, when this isn't found `response` (as a `String`) is checked for a mapping to an `Int`, which is found. The mapping is then attempted and the `Int` is aliased back to an `Input Number`.&#x20;

This mapping will attempt to map the `String` to an `Int` in any way that it can, and if it can't, then an error will be thrown. There are ways to check that a `String` and can be safely mapped to an `Int`, a couple of which are covered in later sections.
{% endhint %}

This initial operation requests a number from the user, then waits for the response, and then evaluates any operations that handle the response as an `Input Number`. Here is an operation to do just that:

```
Notify of an even Input Number: <number> when number % 2 = 0?
    evaluate "That number is even!" as Console Message.
```

This operation will be performed for an evaluated `Input Number` which it will call `number`. `number` must be even (modulus[^1] 2, leaving no remainder) for the operation to be performed, as specified by `when number % 2 = 0`. Upon being performed, the operation sends a console message that the number is even.

It may be nice to also notify the user of when the number is not even, which can be done with this similarly looking operation:

```
Notify of a not even Input Number: <number> when number % 2 = 1?
    evaluate "That number is not even!" as Console Message.
```



### Operations with Multiple Contexts

The initial implementation of the Even Checker doesn't validate whether the user actually provided a number. As is, it would crash for non-numerical input, which is usually not a desired result. The implementation can be updated to use aliases that will make use of Rede's built-in validation operations.

```
Check whether even: when initialized?
    evaluate "Please enter a number." as Console Message,
    evaluate response: Console Response;
    evaluate <response as Unvalidated Int String, 
        validated response: Validated Int String;>,
    evaluate validated response.
```

The initial operation has a new line `evaluate <response as Unvalidated Int String, validated response: Validated Int String;>,` that aliases the response (an already aliased `String`) as an `Unvalidated Int String` (a built-in alias of `String`) and declares a new `validated response` which is another built-in context, `Validated Int String`. Evaluating these two contexts together, as is denoted by the `< >` surrounding them, will initiate an operation that validates the text of the `Unvalidated Int String` and updates the `Validated Int String` with the result.

{% hint style="info" %}
The angle brackets `<  >` around contexts actually builds an instance of a built-in type, in Rede, called an "operable". These can be evaluated to initiate operations that use all of the contexts contained within the operable and are reviewed in more detail in [a later chapter](../chapter-6-abstracting-evaluations/6.1-composing-operables.md).
{% endhint %}

`Validated Int String` is a `String` context with one [extension property](../chapter-2-creating-context/2.3-adaptation.md#extending), `Is Valid Int`, which is a `Bool` (true or false). The value of the context itself is the `String` that was validated. If the value can safely be converted to an `Int` then `Is Valid Int` is `True`. Knowing that, a new operation can be made to be performed from the initial operation's `evaluate validated response`:

```
Initiate Validated Int String check: <input> when input (Is Valid Int)?
    evaluate input to Input Number.
```

The above operation will map the user's input `String` to the `Input Number` context and evaluate that, which will initiate either `Notify of an even Input Number` or `Notify of a not even Input Number` operation.

One benefit to this method of input validation is the ability to handle the invalid case, such as with this operation:

```
Notify user of bad input for Validated Int String: <input> 
    when not input (Is Valid Int)?
        evaluate "\(input) is not a number." as Console Message.
```

{% hint style="danger" %}
Notice how functionality was added only by adding this new operation. In the traditional implementation of most other paradigms, this isn't the case. It is usually not sufficient to add only the new functionality, as the programmer must also change any related functionality that will use it. Contextual Programming decouples functionality from other functionality through the use of `evaluate` which indirectly executes the operations that qualify for the specified contexts.

As such Contextual Programming only requires changes beyond the new functionality when there are new contexts to work with or in some cases when contexts are changed.
{% endhint %}



## Qualifying Value Declarations

The validation performed in the last section is one way to ensure the application does not crash when converting invalid input to the expected data type. Another way this can be done is within an operation's qualifications, as shown with this operation:

```
Initiate even check for Console Response: <response>
    number: response to Input Number?
        evaluate number.
```

This operation will occur for the same `evaluate response: Console Response` that is specified in the initial operation. It will take that user's response and attempt to declare a new value for the operation that maps the response to an `Input Number`. If this fails for any reason, then the operation won't be performed, but the application will not crash, as this failure is treated the same as any other failure for a context's state to qualify for an operation. There are other scenarios in which values can be declared within the qualifications of an operation as well.

With this new operation performing the validation and the mapping, the initial operation can be changed to:

```
Check whether even: when initialized?
    evaluate "Please enter a number." as Console Message,
    evaluate Console Response.
```

{% hint style="info" %}
The last line here has a syntax different than shown with an evaluate so far. `evaluate Console Response` specifies that an instance of `Console Response` will be created, and evaluated, but its value isn't needed for the remainder of this operation, so there is no name specified, and no `:` and `;` for an explicit variable declaration.
{% endhint %}

However, the new operation, as is, might not always perform as desired, for reasons explained in the next section.



## Identifiers and Execution Order

When more than one operation qualifies for evaluated context(s), the order of those operations being performed might matter, or it might not. Looking back at the last section's `Initiate even check for Console Response` operation, it's noteworthy that it takes the same `Console Response` context as some other built-in operation. That built-in operation is the operation that polls for the user's input to the console and then captures it within the `Console Response` context.

Both of these operations taking the same context is what permits the last section's `Even Checker` operation to be so simple, with that `evaluate response: Console Response` line both getting the user's input and initiating the check for that input. However, this only works if the order of those operations' execution occurs as expected; specifically, `Initiate even check for Console Response` must occur after the context is populated with the user's input.

While there are ways to [work around this with an alias](3.2-expanding-on-when.md#conditional-awaits) and another `evaluate`, Rede offers two concepts that provide a more direct solution. The first is an operation identifier. An identifier is an optional part of an operation's declaration that permits the operation to be identified by other operations. The identifier is any text that occurs before `::` in front of the operation's name. The generalized syntax appears as [the following](#user-content-fn-2)[^2]:

```
"Operation Identifier" ::
    "Operation name with [Context Types]": <"Context Names"> "Qualifications"? 
        "Operation Logic".
```

{% hint style="info" %}
The formatting here is irrelevant, but putting the identifier on its own line can elevate its apparent importance to future readers of the code.
{% endhint %}

Then its specific use for this example can be:

```
Check Response :: 
    Initiate even check For Console Response: <response>
        number: response to Input Number?
            evaluate number.
```

This is the same `Initiate even check for Console Response` operation as before, but now it has an identifier, `Check Response`, which allows other operations to refer to this operation.

{% hint style="warning" %}
While operation names are mostly for describing the operation to other programmers, and specifying the required context types, an operation's identifier will be used to refer to the operation in other places, so it needs to be unique (relative to namespacing[^3] and [groups](3.3-operation-groups.md)), but it may be desirable to also not make it too long.
{% endhint %}

With a way to identify operations, there can be Relational Control Flow, where operations define their control flow relative to one another. This control flow is defined by keywords, such as `after` and `before`. Using this control flow, `Check Response` can have its order relative to the identified `Capture Console Input As Response` specified explicitly:

```
Check Response :: 
    Initiate even check for Console Response: <response>
        after Capture Console Input as response,
        number: response to Input Number?
            evaluate number.
```

{% hint style="info" %}
While `Check Response` does not really need its identifier here, it's a good practice to provide identifiers for operations that are relational to other specific operations, if only for future ease of extending that control flow with more operations. In general, operations without identifiers are not open to relational control flow, so whether an operation has an identifier should depend on the intended use of its codebase or whether the responsibilities of its contexts should be able to be extended through related operations.
{% endhint %}

There is a statement for Relational Control Flow in Rede that is sufficient in this specific use case and removes the necessity for the identifiers though:

```
Initiate even check for Console Response: <response> after all,
    number: response to Input Number?
        evaluate number.
```

In specifying `all` after `after`, `Initiate even check for Console Response` will occur after all other operations for `Console Response` have completed. Be careful though, this is not very extendible with more operations, as this operation will now always try to occur last (or at the same time as another operation that wants to be last).



## Conditional Evaluations

The last section offers a more concise version of this implementation than that of the earlier sections, but it lacks the ability to notify the user of invalid input, which is something that was achievable in earlier sections. Handling invalid input can be accomplished while still keeping much of the latest changes, but there need to be more changes in a few places, so let's review the code as it currently is:

```
Input Number: context Int.

Check whether even: when initialized?
    evaluate "Please enter a number." as Console Message,
    evaluate Console Response.
    
Initiate even check for Console Response: <response> after all,
    number: response to Input Number?
        evaluate number.

Notify of an even Input Number: <number> when number % 2 = 0?
    evaluate "That number is even!" as Console Message.

Notify of a not even Input Number: <number> when number % 2 = 1?
    evaluate "That number is not even!" as Console Message.
```

The first change is to break the direct connection between the validity check and `Console Response`. This can be done with an alias:

```
Potential Number Response: context Console Response.
```

Now the validation operation can use this alias instead of the relational control flow:

```
Initiate even check for Potential Number Response: <response>,
    number: response to Input Number?
        evaluate number.
```

With an accompanying change to the initial operation:

```
Check whether even: when initialized?
    evaluate  "Please enter a number." as Console Message,
    evaluate response: Console Response;
    evaluate response as Potential Number Response.
```

Now comes the work to handle the invalid input. There will need to be one more alias for invalid input:

```
Invalid Number Response: context Potential Number Response
```

Then the operation itself:

```
Notify user of Invalid Number Response: <response>?
        evaluate "\(response) is not a number." as Console Message.
```

Finally, the last update to the initial operation:

```
Check whether even: when initialized?
    evaluate  "Please enter a number." as Console Message,
    evaluate response: Console Response;
    evaluate response as Potential Number Response 
        or response as Invalid Number Response.
```

This last update changed `evaluate response as Potential Number Response` to `evaluate response as Potential Number Response or response as Invalid Number Response`. The noteworthy part of this change is the `or`, which defines the context(s) that follow as conditional evaluations to be performed. It basically says "if no operation occurs for `response as Potential Number Response`, then evaluate `response as Invalid Number Response`.

This line results in `response` being aliased as `Potential Number Response` which will attempt to perform the `Initiate even check for Potential Number Response` operation. If that operation does not qualify, then `response` will be aliased as `Invalid Number Response` and `Notify user of Invalid Number Response` will be performed instead.

{% hint style="info" %}
The opposite conditional `evaluate` can be achieved with `and`, in which the following context(s) will only be evaluated by the `evaluate` if the preceding evaluation had successfully performed at least one operation.
{% endhint %}

The use of conditional evaluates allows chaining of evaluations in a logical fashion that furthers the concept of "when" logic occurs.

[^1]: Modulus is represented by `%` in Rede, as it is in many other languages as well.

[^2]: As before, text within double quotations here would be replaced by text meaningful to an actual declaration. Text within square brackets signifies that specific names defined within the code should replace the text.

[^3]: Namespacing is a way to group different program constructs together and to designate which group of constructs another part of code is explicitly using.
