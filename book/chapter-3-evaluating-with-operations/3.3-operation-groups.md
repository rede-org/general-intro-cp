# 3.3  Operation Groups

## The Purpose of Groups

In some circumstances, as seen in the previous example, operations may [depend on one another](3.2-expanding-on-when.md#identifiers-and-execution-order) or [share the same contexts](3.2-expanding-on-when.md#operation-conditions) with a similar intent. It can be helpful to organize such operations into an "Operation Group", which is essentially an operation that encapsulates[^1] a group of operations.

By defining an operation group, all of the encapsulated operations can share the same contexts, while also requiring new contexts specific to themselves, and have overarching qualifications. All Relational Control Flow defined on the encapsulated operations is relative within the group, so the operations can be more explicit about their relationship with one another. They can also designate one of the operations to be performed in the case that the group qualifies but none of the non-default encapsulated operations do.



## Fizz Buzz

A good example of operation groups is a classic programming challenge, [Fizz Buzz](#user-content-fn-2)[^2].

### Getting the Input

Some of the concepts from last section's input validation will be useful in this program, so there will be some parallels in contexts and some operations. To start, the input-related context should be declared:

```
Fizz Buzz Input String: context Console Response.
Fizz Buzz: context Int.
```

Then the initial operation:

```
Run fizz buzz: when initialized?
    evaluate "Please enter a number." as Console Message,
    evaluate response: Console Response;
    evaluate response as Fizz Buzz Input String.
```

Next, the `Fizz Buzz Input String` can be handled, which is where the first operation group will be used. The generalized syntax for an operation group looks like [the following](#user-content-fn-3)[^3]:

```
"Operation Group Identifier" ::
    "Operation group name with [Context Types]": <"Context Names"> "Qualifications"? 
    {
        [An Encapsulated Operation]
        [Another Encapsulated Operation]
    }
```

{% hint style="info" %}
Notice how the encapsulated operations are surrounded by `{ }`. This is the same as when [declaring contexts](../chapter-2-creating-context/2.1-organizing-data.md#defining-a-context) with multiple properties, and as noted in that section, this is because an operation group is a collection of operations, therefore, the same general syntax is used as when declaring a collection.
{% endhint %}

The specific use of operation groups for the purposes of evaluating `Fizz Buzz Input String` is the following:

```
Evaluate Fizz Buzz Input String: <input>
{
    Initiate fizz buzz:
        fizz buzz: input as Fizz Buzz,
        when fizz buzz > 0?
            evaluate fizz buzz;
    
    Notify of invalid input:
        default?
            evaluate "That input is not valid for Fizz Buzz!" as Console Message;
}.
```

The above operation group encapsulates two operations, `Initiate fizz buzz` and `Notify of invalid input`. The first operation will attempt to convert the input `String` (aliased as `Fizz Buzz Input String`) to an `Int` (aliased as `Fizz Buzz`). If successful, then it will further qualify the operation by requiring that the `Int` be greater than 0.

If the first operation does not qualify, either due to the failed cast or the number not being greater than 0, then the second operation will be performed. Its only qualification is `default`, which specifies that it will only qualify if no other operations in the group have qualified.

These two operations provide input handling, with proper notification to the user for invalid input, and set the stage to perform the actual fizzing and buzzing of the program, per the `evaluate fizz buzz` of `Initiate fizz buzz`.

### Performing Fizz Buzz

With the validated input aliased as `Fizz Buzz`, the core part of the program can be implemented. This can be done in a few steps, the first is to define the operation group:

```
Perform Fizz Buzz: <fizz buzz>
    foreach i in Range[1, fizz buzz]?
{
}.
```

This operation group requires the `Fizz Buzz` context, but there is a new concept in its declaration. The line `foreach i in Range(1, fizz buzz)?` shows a [Data Control Flow](#user-content-fn-4)[^4] qualifier, the `foreach` qualifier. This defines the operation as occurring for every value (called `i` in this case) for a specific range or collection. The range in this case is an inline declaration `Range(1, fizz buzz)` which creates a value for a range of 1 to the value of `fizz buzz`, including both 1 and the maximum value.

With the operation being performed multiple times for the number 1 to `fizz buzz`, it's up to the encapsulated operations to qualify appropriately and output either the current number, "Fizz", "Buzz", or "Fizz Buzz" on each iteration. A good starting point is to focus handling when the number is divisible by 3, and when it isn't.

```
Perform Fizz Buzz: <fizz buzz>
    foreach i in Range[1, fizz buzz]?
{
    Divisible by 3 :: 
        when i % 3?
            evaluate "Fizz" as Console Message;
    Otherwise :: 
        default?
            evaluate i to Console Message;
}.
```

{% hint style="warning" %}
Notice how the encapsulated operations declare an operation identifier (preceding the `::`) but have no operation name. This is acceptable in Rede when an operation requires no contexts, which may often be the case when working with encapsulated operations since their contexts are already declared for the operation group.

This enables programmers to consider only a shortened and unique (within the operation group if applicable) name. In this example, the identifiers are descriptive enough of what is being done by each operation and also work well for the Relational Control Flow qualifiers.
{% endhint %}

Now the operation group has two operations. The first qualifies whenever `i` is divisible by 3, and it outputs "Fizz" to the console. Whenever the first doesn't qualify, the default operation will run, which will output `i` to the console, after converting the number to a `Console Message`.

Handling when `i` is divisible by 5 is easy enough with another operation:

```
Perform Fizz Buzz: <fizz buzz>
    foreach i in Range[1, fizz buzz]?
{
    Divisible by 5 :: 
        when i % 5?
            evaluate it "Buzz" as Console Message;
    Divisible by 3 :: 
        when i % 3?
            evaluate "Fizz" as Console Message;
    Otherwise :: 
        default?
            evaluate i to Console Message;
}.
```

This leaves one last case, when `i` is divisible by both 3 and 5. Whenever this occurs, `i` is also divisible by 15, so one of the simplest and more explicit ways to handle this situation is with another operation:

```
Perform Fizz Buzz: <fizz buzz>
    foreach i in Range[1, fizz buzz]?
{
    Divisible by 15 :: 
        when i % 15?
            evaluate "Fizz Buzz" as Console Message;
    Divisible by 5 :: 
        when i % 5?
            evaluate "Buzz" as Console Message;
    Divisible by 3 :: 
        when i % 3?
            evaluate "Fizz" as Console Message;
    Otherwise :: 
        default?
            evaluate i to Console Message;
}.
```

There's a problem with this code though. As is, whenever `i` is divisible by 15, it will output "Fizz Buzz", but then it will proceed to output "Buzz" and "Fizz". Operations within an operation group are not naturally exclusive to one another being performed; only the default operation will exclude itself upon the performance of at least one other operation.

To amend this issue, another Relation Control Flow qualifier can be used to specify that `Divisible by 5` and `Divisible by 3` should not qualify if `Divisible by 15` does. This new qualifier is the [`without` qualifier](#user-content-fn-5)[^5], which works similarly to the `after` qualifier. Here is the final version of this operation group, using `without`:

```
Perform Fizz Buzz: <fizz buzz>
    foreach i in Range[1, fizz buzz]?
{
    Divisible By 15 :: 
        when i % 15?
            evaluate "Fizz Buzz" as Console Message;
    Divisible By 5 :: 
        when i % 5,
        without Divisible By 15?
            evaluate "Buzz" as Console Message;
    Divisible By 3 :: 
        when i % 3,
        without Divisible By 15?
            evaluate "Fizz" as Console Message;
    Otherwise :: 
        default?
            evaluate i to Console Message;
}.
```

With that, the Fizz Buzz program now works as expected. It asks the user for a number, validates it, then iterates from 1 to that number, outputting what is expected for each number.

[^1]: Encapsulation is another concept that is very important in Object-Oriented Programming. It is the idea that multiple constructs or parts of functionality can be grouped together as one, sometimes with the specific details within that encapsulation being hidden from the outside.

[^2]: Fizz Buzz is a game in which players take turns counting (starting at 1, counting up by 1). In place of any word divisible by 3, a player should say "fizz", and for any word divisible by 5 the player should say "buzz". For any word divisible by both 3 and 5 the player should say "fizz buzz". The program version operates similarly, except the user provides the number that the program counts up to.

[^3]: As before, text within double quotations here would be replaced by text meaningful to an actual declaration. Text within square brackets signifies that specific names or constructs defined within the code should replace the text.

[^4]: There are several of these qualifiers that will be covered later. Essentially, they enable operations to be performed on data (often a collection of some kind) under various conditions specific to that data.

[^5]: As with `after` having its `before` counterpart, `without` has a `with` counterpart, in which the `with` specified operation will only be performed if its identified operation is also performed.
