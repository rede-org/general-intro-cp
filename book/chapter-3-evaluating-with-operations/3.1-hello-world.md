# 3.1  Hello World!

## Overview

For an application to progress, the state of its contexts must be changed. Operations are the constructs that, at a high-level, define when those contexts are changed/created, and at a more detailed level, how they are changed.

In general, a simple operation that has no required contexts has a declaration that looks like [the following](#user-content-fn-1)[^1]:

<pre><code>"Operation Name" :: <a data-footnote-ref href="#user-content-fn-2">operation</a> "Qualifications"? 'Operation Logic'.
</code></pre>

Qualifications are where the 'when' aspect of Contextual Programming is defined. Statements after the `::` and before the `?` are all considered qualifications and often take the form of `when "some condition"`, but they can also have other forms that will be shown later.

Operation logic is a series of statements that either create new contexts or directly manipulate the operation's contexts (if it has any). The operation logic is actually optional, leaving the operation name and its qualifications as the only required parts of an operation. Such qualification-only operations are usually used in a hierarchy, through a kind of inheritance, to add qualifications to other operations.



## A Simple Operation

One of the simplest examples of an operation is Rede's (simplest[^3]) implementation of the classic "Hello World!" program:

```
Output Hello World :: 
    operation when initialized?
        evaluate "Hello World!" as Console Message.
```

In [the first line](#user-content-fn-4)[^4], `Output Hello World: when initialized?`, there's the operation name, `Output Hello World`, and one qualification, `when initialized`. This qualification is referred to as a Circumstantial[^5] [Control Flow](#user-content-fn-6)[^6] qualification. When applied in this way, a `when initialized` will qualify for execution when the application starts, so this operation called `Output Hello World` will execute immediately when the application begins to execute.

The second line, `evaluate "Hello World!" as Console Message.`, is the operation logic. This is what will actually be executed when `Output Hello World` qualifies and executes. There's a lot going on in this line:

* `evaluate` is a command to the [runtime of the application](#user-content-fn-7)[^7]. It tells the runtime to perform operations that are qualified by the context(s) that follows it. `evaluate` will require the runtime to perform any such operations before the current operation continues, unlike its counterpart `\evaluate` , which is not concerned about when the runtime will perform the operations. There are circumstances when `\evaluate` is more appropriate than `evaluate`, primarily when what is expected to be accomplished by `\evaluate` is of no importance to the current chain of logic or should be done entirely asynchronously[^8].
* `"Hello World!"` is a string literal, which is to say it is an instance of a string[^9] type. This is defining the text that will be presented to the user.
* `as Console Message` is a command to [alias](../chapter-2-creating-context/2.3-adaptation.md#aliasing) the string `"Hello World!"` as a `Console Message` context type, which is a built-in context provided by the runtime. This will be done prior to the `evaluate` command being evaluated by the runtime.

{% hint style="info" %}
There are a few runtime commands like `evaluate` in Rede. For any of them, prepending `\` will make them run asynchronous of the current operation.
{% endhint %}

Altogether, `evaluate "Hello World!" as Console Message.` is telling the application runtime to "take the text, 'Hello World!', alias it as a Console Message, and execute any operations that qualify for that context". The standard functionality of the runtime includes an operation that will qualify for a single `Console Message` and will perform logic that will result in `Hello World!` being displayed to the user through a console.

While not much, this operation shows how to send messages to the user through the console while following the practices of Contextual Programming.



## Requiring Context

Most operations will require at least one context, as the purpose of most operations is to progress the application's state. To specify the required contexts, the operation declaration changes some to look like [the following](#user-content-fn-10)[^10]:

```
"Operation Name" :: "operation description with [Context Types]" : operation 
    <"Context Names"> "Qualifications"? 
        'Operation Logic'.
```

{% hint style="warning" %}
The \[Context Types] that appear in the operation's description define the number of the operation's parameters (the "Context Names" above).
{% endhint %}

{% hint style="info" %}
The parameters (context names) of the operation can be anything. It is recommended that they describe the role of that parameter as it pertains to how its matching context type is described as being used in the operation's description.
{% endhint %}



## Greeting the User

The Hello World example can be modified to greet the user. One way to accomplish this is with a new context and an operation to define its state. It's not necessary to do it this way, but this implementation is clearer and more reusable.

First, the new [context](../chapter-2-creating-context/2.1-organizing-data.md#defining-a-context):

```
Input User Name :: the full name of the user as a String and a String : context
    First Name, Last Name.
```

Next, the updated initial operation:

```
Output Hello User :: 
    operation when initialized?
        evaluate name is Input User Name,
        evaluate "Hello \(name (First Name)) \(name (Last Name))!" 
            as Console Message.
```

There are a few things different here. The second line is noteworthy in that it is evaluating an operation (which is defined below) but is doing so while defining a new context. The context will be referred to as `name` and will be of the new `Input User Name` type, and it will be immediately evaluated.

The other difference is on the third line (which is wrapped to the fourth line for easier viewing). This line is similar to the previous `evaluate "Hello World!" as Console Message` but with some other code nested inside the string. In Rede `\( )` denotes additional strings to be formatted within the encapsulating string. The values within the parentheses are basically injected into the string. In this case, if `First Name` of `name` is "A" and `Last Name` of `name` is "B", then the resulting string will be formatted as "Hello A B!" which will then be output to the console.

Finally, the new operation that will populate the evaluated `Input User Name`:

```
Request Input User Name :: 
    asks the user their name to update an Input User Name : operation <input name>?
        evaluate "What is your first name?" as Console Message,
        evaluate first name is Console Response,
        
        evaluate "What is your last name?" as Console Message,
        evaluate last name is Console Response,
        
        input name (First Name, Last Name) is {first name, last name}.
```

{% hint style="warning" %}
Note the punctuation ending each line in this example. Any time a construct is declared, it must end with a period (`.`), except when it is nested within another construct, in which it ends with a semi-colon (`;`). Any value, qualification, or logic statements within a set of such elements of a declaration, must end with a comma (`,`). This structure is intended to be closely related to the use of punctuation in English.

Punctuation is not required to stack in Rede, so `.` supersedes `;` which supersedes `,`.
{% endhint %}

{% hint style="info" %}
Due to how punctuation defines the statements and declarations in Rede, the whitespace in the code (tabs and lines of code) is irrelevant. The entirety of `Request Input User Name` could be on one line, or it could be on the lines that it is on above but with no tabs for indentation, or any other method of formatting. The formatting of whitespace used is assumed for improved readability for most readers, but any formatting in practice is up to the programmer or team to decide.
{% endhint %}

Unlike the initial operation, the only qualification for this operation is that there is an `Input User Name` being evaluated, which will be named `input name` within the scope of this operation. There's a number of evaluates that are similar to what has been seen so far. `Console Response` is the simplest way in Rede to obtain an input through the console and is basically an alias for a `String`, so that is used to receive the user's first and last names.

The only particularly different part of this operation is the last line, where the responses from the user are assigned as the `First Name` and `Last Name` values of the `Input User Name`. The assignment is performed stepping left-to-right in both the group of parameters for `input name` (to the left of `is`) and the collection composed of `first name` and `last name` (to the right of `is`). This line is what populates the `Input User Name` with the user's input first and last names.

Altogether, this new context and these operations will request the first and last name of the user and then output a greeting to them.

[^1]: As before, text within double quotations here would be replaced by text meaningful to an actual declaration and text within single quotations are optional to be replaced by text meaningful to an actual declaration.

[^2]: The word "operation" is required before the "Qualification" to denote this construct as an operation.

[^3]: In programming, there are often multiple ways to accomplish the same goal. In this case, the simplest implementation is the shortest and most streamlined version, but it has less flexibility and provides less control than other implementations.

[^4]: This entire example could be on one line, but for readability and ease of explaining the concepts, it's been split over two lines.

[^5]: Meaning that it qualifies by a circumstance of the application itself, not necessarily the values of any specific state.

[^6]: Control flow is a programming term to describe how the execution path of an application is determined. Most paradigms/languages define control flow through branches (if/else, switch) and loops (while/for), although how the application reaches those branches may sometimes be abstracted away through other means. Most control flow in Contextual Programming is defined through the qualifications of operations.

[^7]: An application's runtime is the core of the application that is defined by the programming language. In the case of Rede, it includes the systems that keep track of the application's contexts, determine what operations should execute when, and ultimately communicates the intent of the programmer (from their code) to the operating system.

[^8]: Asynchronous functionality is logic that is performed at the same time as other logic, sometimes on a separate core of the computer's processor if possible. Performing logic asynchronously can reduce overall execution time to complete a task that can be safely distributed across execution environments (like different cores).

[^9]: Strings are a sequence of characters. They are essentially text.

[^10]: As before, text within double quotations here would be replaced by text meaningful to an actual declaration, with single quotations being the same, but optional. Text within square brackets signifies that specific names defined within the code should replace the text.
