# 6.1  Contracts

## The Purpose of Contracts

Contracts are another mechanism of abstraction, this time over the data types. Any record, context, enum, operable, or composition type can be bound to a contract. A contract can be used in any operation or behavior, in place of any type that is bound to it. This enables an operation/behavior to depend upon a contract (or set of contracts) instead of a specific type.

The logging feature can be improved upon with contracts to show how this mechanism is useful.



## Contracting Contexts

Recall the logging feature as it was [last implemented](#user-content-fn-1)[^1]:

```
Log Level: [Int]
{
    Verbose [0],
    Debug [1],
    Warning [2],
    Error [3]
}.

Log Settings: context
{
    Minimum Level: Log Level [Log Level (Debug)];
}.

Log Message: context String
{
    Level: Log Level;
}.

Console Log Route : context Log Settings.

Run App :: 
    create the default App State, initialize and run until shutdown: {app}?
{
    Initialize :: for {app} when initialized?
        activate Log Message ["App Initialized.", Level [Log Level (Debug)] ];
    
    Run :: for {app} whenever app(Continue Running)?
        activate Log Message ["App is running.", Level [Log Level (Verbose)] ],
        await Console Response,
        activate Log Message ["About to shut down!", Level [Log Level (Warning)] ],
        app(Continue Running) is False;
    
    Shutdown:: for {app} whenever app is shutting down, after all?
        deactivate app;
}.

Maintain Console Log Route ::
    maintain the existence of an optional coexistent Console Log Route in accordance 
        with a shared App State: {route, app}?
{
    Log Stop :: when terminated?
        activate Log Message ["Stopping Logs.", Level [Log Level (Debug)] ];
}.

Perform Route Logging ::
    logging per Console Log Route for shared {*Log Message*}: {route, messages}
        where messages is messages filtered by 
            [(message) => message(Level) >= route(Minimum Level)]?
{
    Valid Log Message: context Log Message;
    
    Initiate Logging :: for {messages} 
        whenever |messages| > 0, 
        foreach message in messages?
            evaluate message as Valid Log Message,
            \deactivate message;
}.

Perform Console Logging ::
    logging per Console Log Route for shared {*Log Message*}: {route, messages}
        extends Perform Route Logging for {route, messages}?
{
    Log :: log a Valid Log Message to the console : <message>?
        evaluate "Log: \(message)" as Console Message;
}.
```

There are a couple of areas where this implementation can still be improved. The first is in the re-use of `Perform Route Logging`. As is, `Perform Route Logging` would need a different implementation for every type of log route, like `Console Log Route` or `File Log Route`, even though its functionality has been abstracted away from any specifics for that context, so one contract that will be needed is for log routes.

Another is the dependency on the specific context type of `Log Message`. There's nothing wrong with `Log Message` itself, but every time a log is created with anything but the default `Log Level`, the `Log Level` must be specified. For something as commonly used as logging, that can end up looking a bit clunky. The deactivation in `Perform Route Logging (Initiate Logging)` is a bit odd as well. The deactivation of the message may be better handled by a behavior more dedicated to that purpose.

Focusing on the former, the first step is to create the new contract:

```
Log Route: contract
{
    Minimum Level: Log Level [Log Level (Debug)];
}
```

Next, `Console Log Route` can be updated to specify that it is bound to the contract:

```
Console Log Route [Log Route] : context Log Settings.
```

Notice the user of the stand-in notation, `[ ]`, to specify that `Log Route` can stand-in for `Console Log Route`, which results in the declared type (`Console Log Route` in this case) being bound to the contract (`Log Route`). Since contracts are automatically fulfilled[^2], `Log Settings` no longer serves a purpose, so it can be removed:

```
Console Log Route [Log Route] : context.
```

{% hint style="danger" %}
Contracts and ancestors serve different purposes in Contextual Programming.

An ancestor is like a starting point for a type [to adapt](../chapter-2-creating-context/2.3-adaptation.md#overview). A descendant can be used where an ancestor is required (they can be cast to one another), but descendants do not qualify for behaviors that require the ancestor type. Ancestors are also inherited; a descendant, `B`, of `A`, will pass on its adaptation (or lack thereof) of `A` to its own descendant, `C`. A descendant can only have one ancestor.

A contract specifies how a type can be used. In the example of the logging feature, `Console Log Route` can be used anywhere there is use of `Log Route`, including for the qualification of a behavior. Since a descendant could potentially break a contract fulfilled by its ancestor (by removing a value), contracts must be explicitly declared for each non-contract type (they are not inherited from an ancestor). However, a type can be bound to multiple contracts.
{% endhint %}

To address the latter concerns outlined above, `Log Message` can be re-implemented as a contract with new contexts that fulfill it:

<pre><code>Log Message: <a data-footnote-ref href="#user-content-fn-3">contract String</a>
{
    Level: Log Level;
}.

Verbose Log Message [Log Message]: context String
{
    Level: <a data-footnote-ref href="#user-content-fn-4">const</a> Log Level [Log Level (Verbose)];
}.

Debug Log Message [Log Message]: context String
{
    Level: const Log Level [Log Level (Debug)];
}.

Warning Log Message [Log Message]: context String
{
    Level: const Log Level [Log Level (Warning)];
}.

Error Log Message [Log Message]: context String
{
    Level: const Log Level [Log Level (Error)];
}.
</code></pre>

One more layer of abstraction will be useful for a generalized approach to deactivating the log messages. This will be started by specifying that the `Log Message` contract itself is bound to a high-level `Message` contract, like so:

```
Message: contract.

Log Message [Message]: contract String
{
    Level: Log Level;
}.
```

{% hint style="warning" %}
A contract bound to one or more contracts is effectively a descendant contract inheriting from its ancestor contract(s). Any non-contracts bound to the descendant will inherit the use of any ancestral contract(s). In effect, with the current example, a `Debug Log Message` fulfills the `Log Message` contract, and in doing so, fulfills the `Message` contract. However, contract descendants cannot remove anything declared by their ancestor(s), a stipulation that enables this form of inheritance between contracts.
{% endhint %}

A `Message` at this high-level isn't concerned with whether there is a specific value being conveyed as the message, so much as it is just specifying that the contracted type will be handled, behaviorally, as a message.

{% hint style="info" %}
Programmers familiar with Object-Oriented Programming, at least as it is implemented in some other languages, might see similarities in contracts with a concept called interfaces. They are similar in purpose and use (respective to the differences of the two paradigms), with the primary difference, at least in Rede, being that contracts are not inherited by non-contract types like interfaces are, which is to accommodate the more flexible adaptation of non-contract descendants.
{% endhint %}



## Abstracting Behavior Contexts

With the contracts in place, the behaviors can be updated. This is done by simply replacing the contexts with their stand-in contracts, like so for `Perform Route Logging`:

```
Perform Route Logging ::
    logging per [Log Route] for shared {*[Log Message]*}: {route, messages}
        where messages is messages filtered by 
            [(message) => message(Level) >= route(Minimum Level)]?
{
    Valid Log Message: context [Log Message];
    
    Initiate Logging :: for {messages} 
        whenever |messages| > 0, 
        foreach message in messages?
            evaluate message as Valid Log Message,
            \deactivate message;
}.
```

{% hint style="warning" %}
Notice how `Valid Log Message` is now a descendant of `[Log Message]`. It isn't a descendant of the contract itself in this case, but a descendant of any type that fulfills the `Log Message` contract, such as the `Debug Log Message` or `Verbose Log Message`. This enables any of those contexts that fulfill `Log Message` to be cast and evaluated as a `Valid Log Message`, without the cast explicitly knowing about the contracted type.
{% endhint %}

The points at which log messages are created and activated must be updated to use the new contexts:

```
Run App :: 
    create the default App State, initialize and run until shutdown: {app}?
{
    Initialize :: for {app} when initialized?
        activate "App Initialized." as Debug Log Message;
    
    Run :: for {app} whenever app(Continue Running)?
        activate "App is running." as Verbose Log Message,
        await Console Response,
        activate "About to shut down!" as Warning Log Message,
        app(Continue Running) is False;
    
    Shutdown:: for {app} whenever app is shutting down, after all?
        deactivate app;
}.

Maintain Console Log Route ::
    maintain the existence of an optional coexistent Console Log Route in accordance 
        with a shared App State: {route, app}?
{
    Log Stop :: when terminated?
        activate "Stopping Logs." as Debug Log Message;
}.
```

That's much cleaner looking now, and it would be easier to find where all log messages of a certain type are coming from, if that were helpful for debugging.

Finally, the message deactivation can be addressed. Having the high-level `Message` contract introduces a concept to this application, that being the Messaging Pattern. This pattern outlines how contexts can serve as messages, that is, a fleeting notification to be handled by various behaviors before an expected destruction. With this pattern, any context can fulfill the `Message` contract to become a literal message upon activation, passing through its behaviors before it is deactivated. There may be variations where a message persists longer than a single stream's evaluation, or where the message-like functionality is more conditional, but this simple implementation works for this use case.

The only real work that needs to be done is a behavior to deactivate messages, as the last functionality to occur for any message's activation stream:

```
Cleanup Messages ::
    deactivate all messages, {*[Message]*}, after their evaluations: {messages}?
{
    Scheduled Message: context [Message];
    
    Initiate Cleanup :: for {messages}
        whenever |messages| > 0,
        foreach message in messages?
            evaluate message as Scheduled Message;
    
    Deactivate :: deactivates a Scheduled Message : {message}
         after all?
             deactivate message;
}.
```

`Cleanup Messages` will be created when the app runs, for an empty bucket that exists to catch any activated contexts that fulfill the `Message` contract. Once activated, upon reactive evaluation, the `Initiate Cleanup` will run and effectively schedule any messages to be deactivated after all of that specific context's other operations have completed.

While unlikely, it is possible that a message could be scheduled multiple times for deactivation. If this weren't using a bucket and a new behavior was created for every message, then that wouldn't be the case, but this persistent behavior offers better performance. This issue is actually an opportunity for additional functionality to this application's concept of a message.

To start, the `Message` contract can include a new value:

```
Message: contract
{
    Will Be Deactivated: Bool [Fase];
}.
```

This new value, `Will Be Deactivated` can serve as a flag for whether the message has been scheduled:

```
Cleanup Messages ::
    deactivate all messages, {*[Message]*}, after their evaluations: {messages}
        where messages is messages filtered by 
            [(message) => message(Will Be Deactivated)]?
{
    Scheduled Message: context [Message];
    
    Initiate Cleanup :: for {messages}
        whenever |messages| > 0,
        foreach message in messages?
            message(Will Be Deactivated) is True,
            evaluate message as Scheduled Message;
    
    Deactivate :: deactivates a Scheduled Message : {message}
        when message(Will Be Deactivated),
        after all?
             deactivate message;
}.
```

Now any scheduled messages will be excluded from `Cleanup Messages` bucket of messages and won't be scheduled again. It also enables messages to have their deactivation delayed (one stream's evaluation) before they occur, by setting `Will Be Deactivated` back to `False`. That might be useful in some situations, although it won't be useful for the logs. That value should probably be hidden from the logs actually, to make it clear that they shouldn't delay their scheduled deactivation:

```
Log Message [Message]: contract String
{
    !: replaces Will Be Deactivated;
    Level: Log Level;
}.
```

Now any context that fulfills `Log Message` won't have access to the `Will Be Deactivated` value, which will still exist behind the scenes on the context, being used whenever the context is handled as a `Message`, such as with `Cleanup Messages`.

[^1]: At least the version of it that will be used going forward.

[^2]: Meaning all of the values and decorators specified in the contract will automatically be added to the bound type. The values can be explicitly defined in the bound type as well, or replaced, but they cannot be removed.

[^3]: This is defining that a fulfiller must have an ancestor of `String`.

[^4]: `const` declares a value as being a constant. It cannot be set during creation of the context or changed afterwards.
