# 2.2  Decorators

## Overview

Contexts are primarily structures of data, but those data need to be checked for whether they qualify for operations/behaviors, and new contexts should be able to be produced, at least in part, from existing contexts. Decorators offer an optional way to meet both of these needs for any given context. Contextual Programming defines the following two types of decorators, although a language may [specify others for additional convenience](#user-content-fn-1)[^1].

{% hint style="info" %}
Records can have decorators just as contexts do. This maintains the same purpose as decorators on contexts when records are nested within contexts or to produce new contexts/records based on existing records.
{% endhint %}



## Qualifiers

Qualifiers offer a way to query whether a context qualifies for an operation/behavior, in language appropriate for the purpose of the context. The result of a qualifier will always be a Boolean[^2]. If the result is 'true', it can be said that the context 'qualifies'. How qualifiers are used is shown when operations are covered [in more detail](../chapter-3-evaluating-with-operations/3.2-expanding-on-when.md#operation-conditions).

In general, a qualifier can be declared, within a context, following [this pattern](#user-content-fn-3)[^3]:

```
this "text with optional 'type name' for parameters": 
    ('parameter names, as needed') => "logic that evaluates to a boolean"
```

{% hint style="warning" %}
'this' is a keyword when used in this kind of declaration. It represents the context itself and must be the first word of a qualifier.
{% endhint %}

### A Parameterless Example

A simple, parameterless, example building upon a context declared earlier:

<pre><code>Some Int :: Int : context;
    this is even: () => <a data-footnote-ref href="#user-content-fn-4">this % 2 = 0</a>.
</code></pre>

{% hint style="warning" %}
Equality in Rede is checked for using a single '='. This is different than many other languages that use double equals, '=='.
{% endhint %}

### An Example with Parameters

An example with parameters, again building upon a context declared earlier:

```
Position :: a 2D location represented by Int and Int : context x, y;
    this matches Int and Int: (x, y) => this(x) = x && this(y) = y.
```

{% hint style="warning" %}
The 'x' and 'y' in "(x, y)" are associated with the two Ints in the declaration name. As with the context declaration, parameters in this way match, by order, with the types in the name.
{% endhint %}

{% hint style="info" %}
Throughout Rede, almost anything wrapped in parentheses is considered a "detail of", meaning something specific to the preceding declaration or value, the primary exception being when the order of operations is being clarified through parentheses.

In this example, 'x' and 'y' are details of the Ints in the declaration name, they specify a name to associate with each Int, whereas "this(x)" is specifying the retrieval of the property 'x', which is a detail of the Position context.
{% endhint %}



## Operators

A common operator that most people are familiar with is '+', the addition operator. As with many programming operators, it has its roots in mathematics, and like its use in mathematics, it takes two instances[^5] (being numbers[^6] in math) and produces a new instance. Operators in Rede work much the same way.

A type will, by default, have the operators of its ancestor, but it can add to, remove, or replace those operators. This section looks at adding (specific) operators, removal and replacement are covered [later](2.3-adaptation.md).

### A Parameterless Example

There are operators that do not take another instance. A prime example being negation[^7]. Looking again at the Position context from earlier, there's an opportunity to add a negation operator. The below example adds an operator to do so, on the last line:

```
Position :: a 2D location represented by Int and Int : context x, y;
    this matches Int and Int: (x, y) => this(x) = x && this(y) = y;
    -this: () => Position 
        [ 
            x[-this(x)], 
            y[-this(y)]
        ].
```

{% hint style="info" %}
In the above example, the "stand-in" syntax is being used to specify the values of a new Position's `x` and `y`. That new Position instance is the result of the operator. It has the negated value of the original instance's `x` for its own `x` and the negated value of the original instance's `y` for its own `y`.
{% endhint %}

With this operator declared, negating a Position with code such as `-some position` will provide an expected and appropriate result.

### An Example with Parameters

The Position example can be extended again with an operator that takes a parameter, like so:

```
Position :: a 2D location represented by Int and Int : context x, y;
    this matches Int and Int: (x, y) => this(x) = x && this(y) = y;
    -this: () => Position [ x[-this(x)], y[-this(y)] ];
    this + Position: (other) => Position 
        [ 
            x[this(x) + other(x)], 
            y[this(y) + other(y)] 
        ].
```

Now a Position can be added to another Position through code such as `some position + another position`, with the result being the actual addition of those two Positions.

[^1]: For example, Rede offers Modifiers, as syntactic sugar to alter several of a context's properties together.

[^2]: A value that is either 'true' or 'false'.

[^3]: As before, text within double quotations here would be replaced by text meaningful to an actual declaration. Text within single quotations is additional optional text to be specified.

[^4]: This code is a statement that means "the value of this Some Int, modulus 2, equals 0", the result of which (a boolean) will be returned. It will always be 'true' for any Some Int that is divisible by 2 (any even number) and 'false' for any other value.

[^5]: An instance is a workable construct whose properties are defined by a type. An instance of a `Position` would be able to have its `x` and `y` properties set, and those values persist and can be referenced, but only for that instance where it is used, not for all `Positions`.

[^6]: The concept of numbers is also good to think about for instances. Think of a `Number` as a type. As a concept, a `Number` is a specific type of value that can be added/subtracted. A literal number, like `2` is an instance of a `Number`. It's what can actually be worked with, within the bounds of what it means to be a `Number`.

[^7]: As may be expected, negation should negate a value, like turning 1 to -1.
